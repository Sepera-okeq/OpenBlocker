buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath 'tech.justagod:cutter:2.1.0-SNAPSHOT'
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.2"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'cutter'
apply plugin: "com.github.johnrengelman.shadow"

version = "2.0.3"
group = "ru.will0376.OpenBlocker"
archivesBaseName = "OpenBlocker"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
minecraft {
    version = "1.12.2-14.23.5.2800"
    runDir = "run"
    mappings = "stable_39"
    clientJvmArgs += "-Dfml.coreMods.load=ru.will0376.OpenBlocker.common.Hookloader"
    serverJvmArgs += "-Dfml.coreMods.load=ru.will0376.OpenBlocker.common.Hookloader"
}
configurations.all {
    transitive = false
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://dvs1.progwml6.com/files/maven" } //JEI
    maven { url "https://repo.spring.io/plugins-release/"}
    maven { url "http://dist.wso2.org/maven2/"}
}

cutter.initializeDefault(true, false)

dependencies {
    compile fileTree(dir: 'lib', include: ['*.jar'])

    compile 'commons-cli:commons-cli:1.4'
    compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    compile 'com.google.code.gson:gson:2.7'
    compile group: 'com.google.guava', name: 'guava', version: '21.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    compile group: 'org.ow2.asm', name: 'asm-all', version: '5.2'
    compile group: 'commons-io', name: 'commons-io', version: '2.7'
    compileOnly 'org.projectlombok:lombok:1.18.8'
}
shadowJar {
    classifier = null
    relocate('org.apache.commons.cli', 'shadow.relocate.commons.cli')
    dependencies {
        exclude(dependency('org.apache.commons:commons-lang3:3.9'))
        exclude(dependency('commons-io:commons-io:2.7'))
        exclude(dependency('org.ow2.asm:asm-all:5.2'))
        exclude(dependency('javax.annotation:javax.annotation-api:1.3.2'))
        exclude(dependency('com.google.code.gson:gson:2.7'))
        exclude(dependency('com.google.guava:guava:21.0'))
    }
    exclude 'industrialcraft_2_2.8.220_ex112.jar'
    exclude 'jei_1.12.2-4.12.1.217.jar'

    exclude '*.html'
    exclude 'dummyThing'
    exclude '*.css'
    exclude '*.js'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    jar {
        manifest {
            attributes 'FMLAT': 'forge_at.cfg'
            attributes 'FMLCorePlugin': 'ru.will0376.OpenBlocker.common.Hookloader'
            attributes 'FMLCorePluginContainsFMLMod': 'true'
        }
    }
}

buildClient.dependsOn shadowJar
buildServer.dependsOn shadowJar